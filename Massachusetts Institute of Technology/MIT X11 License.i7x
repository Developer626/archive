Version 1/101023 of MIT X11 License by Massachusetts Institute of Technology begins here.

"Implements 'license' and 'warranty' command using the MIT X11 License. Also displays license and warranty when
the game begins unless explicitly asked not to."

Section 1G (for Glulx only)

[Look at first two characters in serial number array and kludgily convert to a YYYY year.]

Include (-

[ DeriveSerialNumberYear;
	return 2000+(ROM_GAMESERIAL->0-48)*10+(ROM_GAMESERIAL->1-48);
];

-)

Section 1Z (for Z-machine only)

[Look at first two characters in serial number array and kludgily convert to a YYYY year.]

Include (-

[ DeriveSerialNumberYear;
	return 2000+(HDR_GAMESERIAL->0-48)*10+(HDR_GAMESERIAL->1-48);
];

-)


Section 2a -Flag to allow suppression of extension warnings in non-release builds

Suppress X11 license warnings is a truth state that varies. 

Section 2b - Warnings for values not defined that affect behavior - not for release

[Resetting the story creation year is no longer allowed as of 6E59, so this block is not useful...
When play begins while suppress X11 story creation year autoset is not true and story creation year is 0 and suppress X11 license warnings is not true (this is the notify authors about autoset for story creation year rule):
	say "[bold type]*** NON-RELEASE VERSION WARNING ***[line break][italic type]The MIT X11 License
	extension is assuming the compilation year is equivalent to the story creation year, and is setting the
	story creation year accordingly. To suppress this behavior, set the [']suppress X11 story creation year
	autoset['] truth value provided by this extension to true in your source code. If you explicitly set the
	story creation year in your source code, this warning will not be shown.[roman type][paragraph break]"
]

When play begins while copyright year string is empty and story creation year is 0 and suppress X11 license warnings is not true (this is the warn authors if story creation year is not set rule):
	say "[bold type]*** NON-RELEASE VERSION WARNING ***[line break][italic type]The story creation
	year for this work has not been set. The copyright notice generated by the MIT X11 License extension
	will default to compilation year as the copyright year unless the author explicitly sets the story creation
	year in the source code, or the author explicitly sets the [']copyright year string['] text value provided
	by this extension in the source code.[roman type][paragraph break]"

When play begins while copyright holder is empty and suppress X11 license warnings is not true (this is the warn authors if copyright holder is not set rule):
	say "[bold type]*** NON-RELEASE VERSION WARNING ***[line break][italic type]The author has not
	provided an explicit value for the copyright holder of this work, so the MIT X11 License extension is
	using the story author as a default value.[roman type][paragraph break]"

The copyright holder defaults to story author rule is listed after the warn authors if copyright holder is not set rule in the when play begins rules.

[Resetting the story creation year is no longer allowed as of 6E59, so this block is not useful...
The automatically set story creation year if not suppressed rule is listed after the notify authors about autoset for story creation year rule in the when play begins rules.

The automatically set story creation year if not suppressed rule is listed before the warn authors if story creation year is not set rule in the when play begins rules.
]

The print X11 license and warranty on startup rule is listed after the warn authors if story creation year is not set rule in the when play begins rules.

The print X11 license and warranty on startup rule is listed after the warn authors if copyright holder is not set rule in the when play begins rules.

Section 3 - Set copyright holder as story author if not explicitly provided

Copyright holder is a text that varies.

When play begins while copyright holder is empty (this is the copyright holder defaults to story author rule), now copyright holder is the story author.

Section 4 - Set copyright year as compilation year if not explicitly provided

Assumed copyright year is a number that varies.

To decide what number is serial number year: (- DeriveSerialNumberYear(); -).

[Resetting the story creation year is no longer allowed as of 6E59, so this block is not useful...
Suppress X11 story creation year autoset is a truth state that varies.

When play begins (this is the automatically set story creation year if not suppressed rule):
	change assumed copyright year to serial number year;
	if suppress X11 story creation year autoset is not true and story creation year is 0, change story creation year to assumed copyright year.
]

When play begins (this is the automatically set assumed copyright year rule):
	change assumed copyright year to serial number year.

Section 5 - Saying the copyright notice

Copyright year string is a text that varies. [deliberately left blank, author may set in story file if desired]

To say copyright notice:
	say "Copyright (c) [if copyright year string is not empty][copyright year string] [else if story creation year is not 0][story creation year] [else][assumed copyright year] [end if][copyright holder]".

Section 6 - Automated announcement at start of game

Suppress X11 license banner is a truth state that varies.

To display license and warranty:
	display the license;
	say "[line break]";
	display the warranty.

When play begins while suppress X11 license banner is not true (this is the print X11 license and warranty on startup rule), display license and warranty.

The print X11 license and warranty on startup rule is listed after the automatically set assumed copyright year rule in the when play begins rules.

Section 7 - License command

Requesting the license is an action applying to nothing. Understand "license" as requesting the license.

After requesting the license (this is the display X11 license on request rule), display the license.

To display the license:
	 say "[copyright notice][paragraph break]Permission is hereby granted, free of charge, to any person  obtaining a
	copy of this software and associated documentation files (the 'Software'), to deal in the Software without restriction,
	including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
	copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following
	conditions:[paragraph break]The above copyright notice and this permission notice shall be included in all copies
	or substantial portions of the Software."

Section 8 - Warranty command

Requesting the warranty is an action applying to nothing. Understand "warranty" as requesting the warranty.

After requesting the warranty (this is the display X11 warranty on request rule), display the warranty.

To display the warranty:
	say "THE SOFTWARE IS PROVIDED 'AS IS', WITHOUT WARRANTY OF ANY KIND,  EXPRESS OR IMPLIED, INCLUDING
	BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
	NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM,
	DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
	FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE."

Section 9 - Self test - not for release

Test X11-License with "license / warranty".

MIT X11 License ends here.

---- DOCUMENTATION ---- 

This extension is designed to make it easy to release your Inform 7 work under the MIT X11 License,
sometimes known informally as the "MIT license". It is based on the information found at:

	http://en.wikipedia.org/wiki/MIT_license

as of May 25, 2010.

This extension implements two commands:

	"license" - which displays the MIT X11 License, including a copyright notice but excluding warranty information
	"warranty" - which displays the warranty defined with the MIT X11 License

It also includes rules to display both the license and warranty automatically when play begins.

This extension offers customization of its function as follows:

	* The copyright notice displayed automatically includes the copyright year based on either the I7 'story creation
	year' variable (defined as part of bibliographic data) or the 'copyright year string' text provided in this extension.
	If both of these variables are left undefined, the compilation year will be used as the basis for determining the
	copyright year, and a warning will be printed at the start of a non-release build using this extension to notify the
	author. If both variables are defined, the 'copyright year string' will be displayed.

	* The copyright holder of a work may not be the same as the author. This extension includes a variable called
	'copyright holder' that the author may set explicitly, to have entities other than the author shown as the
	copyright holder in the copyright notice. If this variable is left undefined, a rule in the extension will set it to
	the story author by default, and a warning will be printed at the start of a non-release build using this extension
	to remind the author.

	* For authors who don't want to set the above variables but also don't want to see the warnings, a truth state
	variable called 'suppress X11 license warnings' is provided by the extension. If the author sets this to true in
	the story source, these warnings will not be shown even in a non-release build.


